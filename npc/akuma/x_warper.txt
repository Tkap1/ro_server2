
-	script	X Warper	FAKE_NPC,{
	
	if(!.init)
	{
		.init = true;
		
		setarray(.options$[0],
			"Payon Dungeon 1",
			"Payon Dungeon 2",
			"Payon Dungeon 3",
			"Payon Dungeon 4",
			"Payon Dungeon 5",
			"Byalan Dungeon 1",
			"Byalan Dungeon 2",
			"Byalan Dungeon 3",
			"Byalan Dungeon 4",
			"Byalan Dungeon 5",
			"Byalan Dungeon 6"
		);
		setarray(.map_name$[0],
			"pay_dun00x",
			"pay_dun01x",
			"pay_dun02x",
			"pay_dun03x",
			"pay_dun04x",
			"iz_dun00x",
			"iz_dun01x",
			"iz_dun02x",
			"iz_dun03x",
			"iz_dun04x",
			"iz_dun05x"
		);
		setarray(.map_coords[0],
			21,183, // pay_dun00x
			19,33,
			19,63,
			155,159,
			201,204,
			
			168,168, // iz_dun00x
			253,252,
			236,204,
			32,63,
			26,27,
			141,185
		);
		.name$ = "^00ff00X Warper^000000";
		
		if(last_x_warper_choice < 0 || last_x_warper_choice >= getarraysize(.options$)) { last_x_warper_choice = 0; }
	}
	
	mes(.name$);
	mes("Hey! I can warp you to special dungeons");
	mes("Where do you want to go?");
	
	.@select_menu$ = sprintf("Last warp - [%s]:", .options$[last_x_warper_choice]);
		
	for(.@i = 0; .@i < getarraysize(.options$); .@i++)
	{
		.@select_menu$ += .options$[.@i] + ":";
	}
	.@select_menu$ += "Cancel";
	
	.@warp_index = select(.@select_menu$) - 1;
	if(.@warp_index == 0) // Pressed "Last warp"
	{
		.@warp_index = last_x_warper_choice;
	}
	else if(.@warp_index == getarraysize(.options$) + 1) // Pressed "Cancel"
	{
		end;
	}
	else
	{
		last_x_warper_choice = .@warp_index - 1;
		.@warp_index--;
	}
	warp .map_name$[.@warp_index], .map_coords[.@warp_index * 2], .map_coords[.@warp_index * 2 + 1];
	
}

prontera,161,184,3	duplicate(X Warper)	X Warper#prt	1_M_INNKEEPER