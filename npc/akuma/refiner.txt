
prontera,148,175,4	script	Refiner	4_M_DWARF,{
	callsub(on_talk);
	end;
	
OnInit:
	.npc_name$ = "[ ^990099Refiner^000000 ]";
end;

on_talk:
	
	disable_items();
	mes(.npc_name$);
	mes("Select the item that you want to refine");
	
	setarray(.@position$[0], "Accessory 1", "Accessory 2", "Boots", "Garment", "Lower Headgear", "Middle Headgear", "Upper Headgear", "Armor", "Left Hand", "Right Hand");
	.@menu$ = "";
	for (.@i = 0; .@i < 10; ++.@i)
	{
		if(getequipisequiped(.@i))
		{
			.@menu$ += "[" + .@position$[.@i] + "] - " + getequipname(.@i) + ":";
		}
		else
		{
			.@menu$ += "^ff0000[" + .@position$[.@i] + "] - " + "Empty" + "^000000:";
		}
	}
	
	// Select the part.
	.@equip_index = select(.@menu$) - 1;
	
	.@turbo_refine = false;
	.@turbo_goal = 0;
	.@safe_mode = false;
	
	while(true)
	{
		.@refineitemid = getequipid(.@equip_index); // save id of the item
		.@refine_count = getequiprefinerycnt(.@equip_index); //save refinery count
		.@weapon_level = getequipweaponlv(.@equip_index);
		
		// Check if it's worn.
		if (!getequipisequiped(.@equip_index)) {
			clear();
			mes(.npc_name$);
			mes("Brah, your item ain't equipped.");
			close();
		} 
		//Check if the item is refinable...
		if(!getequipisenableref(.@equip_index)) {
			clear();
			mes(.npc_name$);
			mes "I don't think I can";
			mes "refine this item at all...";
			close;
		}

		if(.@refine_count == 20)
		{
			clear();
			mes(.npc_name$);
			mes "I can't refine that";
			mes "item anymore!";
			close;
		}
		
		if(.@refine_count < 10)
		{
			switch(.@weapon_level)
			{
				case 0: 	//Refine Armor
					.@price = 2000;
					.@material = 985;
					break;
				case 1: 	//Refine Level 1 Weapon
					.@price = 50;
					.@material = 1010;
					break;
				case 2: 	//Refine Level 2 Weapon
					.@price = 200;
					.@material = 1011;
					break;
				case 3: 	//Refine Level 3 Weapon
					.@price = 5000;
					.@material = 984;
					break;
				case 4: 	//Refine Level 4 Weapon
					.@price = 20000;
					.@material = 984;
					break;
				case 5: 	//Refine other stuff?
					.@price = 2000;
					.@material = 985;
					break;
			}
		}
		
		else
		{
			if (.@weapon_level >= 1 && .@weapon_level <= 4)
			{
				.@material = 6224; //Bradium
				.@price = 100000;
			}
			else
			{
				.@material = 6223; //Carnium
				.@price = 100000;
			}
		}
		
		.@success_chance = getequippercentrefinery(.@equip_index);
		.@material_count_on_safe_fail = ((.@refine_count + 1) * 2) * (.@refine_count + 1) * (.@weapon_level == 0 ? 4 : .@weapon_level) + 10;
		if(.@refine_count >= 10) {.@material_count_on_safe_fail = .@material_count_on_safe_fail / 4; }
	
		clear();
		mes(.npc_name$);
		mes("Zeny cost: " + "^0000ff" + .@price + "^000000");
		mes(sprintf("Items needed: ^ff0000%d^000000 ^0000ff%s^000000", 1, getitemname(.@material)));
		mes(sprintf("Items needed on safe failure: ^ff0000%d^000000 ^0000ff%s^000000", .@material_count_on_safe_fail, getitemname(.@material)));
		mes("Chance of success: " + ((.@success_chance == 100) ? "^00ff00" + .@success_chance + "^000000" : "^ff0000" + .@success_chance + "^000000"));
		
		if(!.@turbo_refine)
		{
			.@refine_option = select("Upgrade:Safe Upgrade:Turbo Upgrade:Turbo Safe Upgrade:Exit");
		}
		
		if(.@refine_option == 5)
		{
			clear();
			mes(.npc_name$);
			mes("Pussy");
			close();
		}
		
		if(.@refine_option == 2 || .@refine_option == 4) { .@safe_mode = true; }
		
		if(!.@turbo_refine)
		{
			if(.@refine_option == 3 || .@refine_option == 4)
			{
				.@turbo_refine = true;
				mes("Tell me at what refine level you want the item");
				input(.@turbo_goal, 0, 20);
			}
		}
		
		if(.@turbo_refine && .@refine_count >= .@turbo_goal)
		{
			clear();
			mes(.npc_name$);
			mes("It's done!");
			close();
		}
		
		.@temp_material_amount_needed = (.@safe_mode ? .@material_count_on_safe_fail : 1);
		
		// Check for materials
		if(Zeny < .@price)
		{
			clear();
			mes(.npc_name$);
			mes("You dont have enough zeny!");
			close();
		}
		
		if(countitem(.@material) < .@temp_material_amount_needed)
		{
			clear();
			mes(.npc_name$);
			mes("You dont have enoguh " + getitemname(.@material) + "!");
			close();
		}
		
		// Take the materials and zeny
		delitem(.@material, 1);
		Zeny -= .@price;
		
		if(rand(1,100) > .@success_chance)
		{
			clear();
			mes(.npc_name$);
			mes("A BUM!");
			mes("The refine attempt was a failure!");
			if(!.@safe_mode)
			{
				failedrefitem(.@equip_index);
				close();
			}
			else
			{
				specialeffect2(EF_REFINEFAIL, AREA);
				delitem(.@material, .@temp_material_amount_needed - 1);
				mes(sprintf("But since you went for the safe option, I managed to save the item with the ^ff0000%d^000000 ^0000ff%s^000000 you gave me", .@temp_material_amount_needed, getitemname(.@material)));
				if(!.@turbo_refine) { next(); }
			}
		}
		
		else
		{
			successrefitem(.@equip_index);
		}
	
	}
	
end;

}